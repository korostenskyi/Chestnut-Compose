plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlinx-serialization'
}

android {
    namespace 'io.korostenskyi.chestnut'
    compileSdk 33
    defaultConfig {
        applicationId "io.korostenskyi.chestnut"
        minSdk 23
        targetSdk 33
        versionCode buildCode
        versionName buildName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file('.signing/release.properties')
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile rootProject.file('.signing/release.jks')
                storePassword trimKeystoreValue(keystoreProperties['storePassword'])
                keyAlias trimKeystoreValue(keystoreProperties['keyAlias'])
                keyPassword trimKeystoreValue(keystoreProperties['keyPassword'])
            } else {
                println 'Could not find release.properties'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "Boolean", "ENABLE_SENTRY", "true"
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "ENABLE_SENTRY", "false"
            versionNameSuffix "-debug"
        }
    }
    defaultConfig {
        buildConfigField("String", "TMDB_BASE_URL", "https://api.themoviedb.org/3/")
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    def buildTypeDimension = "buildType"
    flavorDimensions buildTypeDimension
    productFlavors {
        production {
            dimension buildTypeDimension
            def keystorePropertiesFile = rootProject.file('config/release.properties')
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                buildConfigField "String", "TMDB_API_KEY", "\"${keystoreProperties['TMBD_KEY']}\""
                buildConfigField "String", "SENTRY_DNS_URL", "\"${keystoreProperties['SENTRY_DNS_URL']}\""
            } else {
                println 'Could not find release.properties'
            }
        }
        develop {
            dimension buildTypeDimension
            applicationIdSuffix ".develop"
            def keystorePropertiesFile = rootProject.file('config/dev.properties')
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                buildConfigField "String", "TMDB_API_KEY", "\"${keystoreProperties['TMBD_KEY']}\""
                buildConfigField "String", "SENTRY_DNS_URL", "\"${keystoreProperties['SENTRY_DNS_URL']}\""
            } else {
                println 'Could not find dev.properties'
            }
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

static String trimKeystoreValue(String value) {
    return value.replaceAll("^\"|\"\$", "")
}

dependencies {
    // Android
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material3:material3:1.2.0-alpha02"
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation 'androidx.paging:paging-compose:1.0.0-alpha20'
    implementation 'androidx.navigation:navigation-compose:2.7.0-alpha01'

    // Accompanist
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.20.0'
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.20.0'

    // Kotlin
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.2'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.45'
    kapt 'com.google.dagger:hilt-compiler:2.45'
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0'

    // Coil
    implementation 'io.coil-kt:coil-compose:2.4.0'

    // Room
    def room_version = "2.5.1"
    api "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Sentry
    implementation 'io.sentry:sentry-android:5.2.4'

    // Test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
}